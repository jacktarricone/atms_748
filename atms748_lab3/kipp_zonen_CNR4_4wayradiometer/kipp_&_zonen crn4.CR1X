'CR1000X Series
'Created by Eric and Jack
'Date: 02/07/2022

'''''''''''''''''''
'''''' Wiring '''''
'''''''''''''''''''

'1H red
'1L blue
'2H white
'2L black
'3H grey
'3L yellow
'4H brown
'4L green
'5H white
'G black
'G clear
'G clear
'VX1 red
'1L, G jumper
'2L, G jumper
'3L, G jumper
'4L, G jumper

'Declare Variables and Units
Public BattV
Public PTemp_C
Public CNR4(4)
Public CNR4Mult(4)
Public CNR4TC
Public CNR4TK
Public RsNet
Public RlNet
Public Albedo
Public Rn
Public LWUpperCo
Public LWLowerCo

Alias CNR4(1)=SWUpper
Alias CNR4(2)=SWLower
Alias CNR4(3)=LWUpper
Alias CNR4(4)=LWLower
Alias CNR4Mult(1)=SWUpperM
Alias CNR4Mult(2)=SWLowerM
Alias CNR4Mult(3)=LWUpperM
Alias CNR4Mult(4)=LWLowerM

Units BattV=Volts
Units PTemp_C=Deg C
Units CNR4TC=Deg C
Units CNR4TK=K
Units RsNet=W/m^2
Units RlNet=W/m^2
Units Albedo=W/m^2
Units Rn=W/m^2
Units LWUpperCo=W/m^2
Units LWLowerCo=W/m^2
Units SWUpper=W/m^2
Units SWLower=W/m^2
Units LWUpper=W/m^2
Units LWLower=W/m^2

'Calibration coefficients from sensor
'Serial no.:181979
Const pyranometer_up=13.47

'Define Data Tables
DataTable(SlowResponse,True,-1)
	DataInterval(0,60,Sec,10)
	Minimum(1,BattV,FP2,False,False)
	Sample(1,PTemp_C,FP2)
	Average(1,SWUpper,FP2,False)
	Average(1,SWLower,FP2,False)
	Average(1,LWUpper,FP2,False)
	Average(1,LWLower,FP2,False)
	Average(1,CNR4TC,FP2,False)
	Average(1,CNR4TK,FP2,False)
	Average(1,RsNet,FP2,False)
	Average(1,RlNet,FP2,False)
	Average(1,Albedo,FP2,False)
	Average(1,Rn,FP2,False)
	Average(1,LWUpperCo,FP2,False)
	Average(1,LWLowerCo,FP2,False)
EndTable


'Main Program
BeginProg
	'Initialize CNR4 multipliers
	SWUpperM=1000/13.42
	SWLowerM=1000/14.76
	LWUpperM=1000/11.52
	LWLowerM=1000/12.20
	'Main Scan
	Scan(20,Sec,1,0)
		'Default CR1000X Datalogger Battery Voltage measurement 'BattV'
		Battery(BattV)
		'Default CR1000X Datalogger Wiring Panel Temperature measurement 'PTemp_C'
		PanelTemp(PTemp_C,60)
		'CNR4 Net Radiometer measurements 'SWUpper', 'SWLower', 'LWUpper', and 'LWLower'
		VoltDiff(CNR4(1),4,mV200C,1,True,0,60,CNR4Mult(),0)
		'CNR4 thermistor measurement 'CNR4TC'
		BrHalf(CNR4TC,1,mV5000,9,VX1,1,2500,True,0,60,1,0)
		'Convert measured voltage ratio into degrees Celsius
		CNR4TC=1000*(CNR4TC/(1-CNR4TC))
		CNR4TC=1/(1.0295e-3+2.391e-4*LN(CNR4TC)+1.568e-7*(LN(CNR4TC))^3)-273.15
		'Calculate Kelvin temperature of CNR4
		CNR4TK=CNR4TC+273.15
		'Calculate temperature corrected long-wave radiation
		LWUpperCo=LWUpper+5.67e-8*CNR4TK^4
		LWLowerCo=LWLower+5.67e-8*CNR4TK^4
		'Calculate short-wave net radiation
		RsNet=SWUpper-SWLower
		'Calculate long-wave net radiation
		RlNet=LWUpperCo-LWLowerCo
		'Calculate albedo
		Albedo=SWLower/SWUpper
		'Calculate net radiation
		Rn=RsNet+RlNet
		'Call Data Tables and Store Data
		CallTable SlowResponse
	NextScan
EndProg
